import "unknwn.idl";
import "oaidl.idl";
import "ocidl.idl";
import "objidl.idl";
import "strmif.idl";

[uuid("5406193C-E3EC-4E5A-95D3-29EC1EAEA033")]
[helpstring("Flags whitch entries should be logged")]
typedef enum StatisticCaptureFlags
{
    SCF_None = 0,

    SCF_MediaSample     = 0x0001,
    SCF_DataCrc         = 0x0002,   // Generate CRC for MediaSample Data or SRK_IS_Read/SRK_IS_Write
    SCF_Streaming       = 0x0004,   // Start/Stop Streaming

    SCF_IAsyncReader    = 0x0008,   // SRK_AR_xxx
    SCF_IStream         = 0x0010,   // SRK_IS_xxx
    SCF_IMediaSeeking   = 0x0020,   // SRK_MS_xxx
    SCF_IMediaPosition  = 0x0040,   // SRK_MP_xxx
    SCF_IBaseFilter     = 0x0080,   // SRK_BF_xxx
    SCF_IPin            = 0x0100,   // SRK_IP_xxx
    SCF_IMemInputPin    = 0x0200,   // SRK_IMP_xxx
    SCF_IQualityControl = 0x0400,   // SRK_QC_xxx

    SCF_All             = -1
} StatisticCaptureFlags;

[uuid("EFA80B5A-913E-4806-B7BC-3202234FDE70")]
typedef enum StatisticRecordKind
{
	// Core filter events
    SRK_Empty = 0,    // Empty Record
    SRK_MediaSample = SCF_MediaSample,
    SRK_StartStreaming = SCF_Streaming,
    SRK_StopStreaming,

    // IAsyncReader Entries
    SRK_AR_Length = SCF_IAsyncReader,
    SRK_AR_Request,
    SRK_AR_SyncRead,
    SRK_AR_WaitNext,

    // IStream Entries
    SRK_IS_Read = SCF_IStream,
    SRK_IS_Write,
    SRK_IS_Seek,

    // IMediaSeeking Entries
    SRK_MS_SetPositions = SCF_IMediaSeeking,
    SRK_MS_SetRate,
    SRK_MS_SetTimeFormat,

    // IMediaPosition Entries
    SRK_MP_SetCurrentPosition = SCF_IMediaPosition,
    SRK_MP_SetStopTime,
    SRK_MP_SetPrerollTime,
    SRK_MP_SetRate,

	// IBaseFilter entries (some inherited from IMediaFilter)
    SRK_BF_JoinFilterGraph = SCF_IBaseFilter,
    SRK_BF_Pause,
    SRK_BF_Run,
    SRK_BF_SetSyncSource,
    SRK_BF_Stop,

	// IPin entries
    SRK_IP_BeginFlush = SCF_IPin,
    SRK_IP_Connect,
    SRK_IP_Disconnect,
    SRK_IP_EndFlush,
    SRK_IP_EndOfStream,
    SRK_IP_NewSegment,
    SRK_IP_ReceiveConnection,

	// IMemInputpin entries
	SRK_MIP_NotifyAllocator = SCF_IMemInputPin,

	// IQualityControl entries
    SRK_QC_Notify = SCF_IQualityControl,
    SRK_QC_SetSink
} StatisticRecordKind;

[uuid("66AA0068-8140-49BB-8125-FEC5AA2FE9F4")]
typedef struct StatisticRecordEntry
{ 
    // Entry
    __int64 EntryNr;
    StatisticRecordKind EntryKind;

    [helpstring("TimeStamp when this entry was created")]
    __int64 EntryTimeStamp;

    // IMediaSample
    VARIANT_BOOL IsDiscontinuity;
    VARIANT_BOOL IsPreroll;
    VARIANT_BOOL IsSyncPoint;
    VARIANT_BOOL IsMediaTypeChange;

    [helpstring("IMediaSample::GetActualDataLength")]
    long ActualDataLength;

    [helpstring("IMediaSample::GetSize")]
    long BufferSize;

    [helpstring("IMediaSample::GetTime; if < 0 => HRESULT")]
    __int64 StreamTimeStart;
    [helpstring("IMediaSample::GetTime; if < 0 => HRESULT")]
    __int64 StreamTimeStop;

    [helpstring("IMediaSample::GetMediaTime; if < 0 => HRESULT")]
    __int64 MediaTimeStart;
    [helpstring("IMediaSample::GetMediaTime; if < 0 => HRESULT")]
    __int64 MediaTimeStop;

    // IMediaSample2
    [helpstring("Indicates if the sample had the IMediaSample2")]
    VARIANT_BOOL HadIMediaSample2;

    [helpstring("only when IMediaSample2")]
    unsigned long TypeSpecificFlags;

    [helpstring("only when IMediaSample2")]
    unsigned long SampleFlags;

    [helpstring("only when IMediaSample2")]
    unsigned long StreamId;

    // First bytes of the sample
    [helpstring("Count of Data taken from the MediaSample")]
    int nDataCount;

    [unique, size_is(nDataCount), helpstring("First bytes of the Media Sample")]
    byte *aData;

    [helpstring("CRC32 value of the MediaSample Data")]
    DWORD crcData;
} StatisticRecordEntry;

[object, uuid("780E9A9E-0CC8-4EE7-9788-3637D8495C51")]
interface IAnalyzerFilterCallback : IUnknown
{
    [id(1), helpstring("Called after a new entry was added to the statistic")]
    HRESULT OnStatisticNewEntry(unsigned long entryNr);

    [id(2), helpstring("Called after the statistic was resetted")]
    HRESULT OnStatisticResetted();
}

[object, uuid("51039F7F-907D-4E3F-B16F-721BC7317094")]
interface IAnalyzerFilter : IUnknown
{
    [propget, id(1), helpstring("Is the analyzer enabled")] HRESULT Enabled([out, retval] VARIANT_BOOL* pVal);
    [propput, id(1), helpstring("Is the analyzer enabled")] HRESULT Enabled([in] VARIANT_BOOL newVal);

    [propget, id(2), helpstring("Flags which entries should be captured (see StatisticCaptureFlags)")] HRESULT CaptureConfiguration([out, retval] int* pVal);
    [propput, id(2), helpstring("Flags which entries should be captured (see StatisticCaptureFlags)")] HRESULT CaptureConfiguration([in] int newVal);

    [propget, id(3), helpstring("LogFile")] HRESULT LogFile([out, retval] BSTR* pVal);
    [propput, id(3), helpstring("LogFile")] HRESULT LogFile([in] BSTR newVal);

    [propget, id(4), helpstring("Bytes to Capture for a Sample")] HRESULT PreviewSampleByteCount([out, retval] unsigned short* pVal);
    [propput, id(4), helpstring("Bytes to Capture for a Sample")] HRESULT PreviewSampleByteCount([in] unsigned short newVal);

    [id(5), helpstring("Reset Statistic")] HRESULT ResetStatistic();
    [propget, id(6), helpstring("Count of statistic entries")] HRESULT EntryCount([out, retval] __int64 *pVal);
    
    [id(7), helpstring("Get an entry from the statistic (if aData != NULL you need to CoTaskMemFree it)")]
    HRESULT GetEntry([in] __int64 nr, [out, retval] StatisticRecordEntry *pVal);

    [id(8), helpstring("Register or remove a callback for the events of the statistic")]
    HRESULT SetCallback(IAnalyzerFilterCallback* pCallback);
};

[
	uuid("CA6B3460-28B3-4A6E-A7FC-A83CF1DEEC48"), 
    helpstring("Analyzer Filter Type Library"),
	//lcid(0x0409), // EN-US
    version(1.0)
] 
library AnalyzerFilterLib
{
    enum StatisticCaptureFlags;
    enum StatisticRecordKind;
    struct StatisticRecordEntry;
    interface IAnalyzerFilterCallback;
    interface IAnalyzerFilter;

    [uuid("CA6B3460-28B3-4A6E-A7FC-A83CF1DEEC49")]
    coclass AnalyzerFilter
    {
        interface IBaseFilter;
        interface ISpecifyPropertyPages;
        [default] interface IAnalyzerFilter;
    }

    [uuid("CA6B3460-28B3-4A6E-A7FC-A83CF1DEEC4A")]
    coclass AnalyzerWriterFilter
    {
        interface IBaseFilter;
        interface IFileSinkFilter2;
        interface IAMFilterMiscFlags;
        interface IMediaSeeking;
        interface ISpecifyPropertyPages;
        [default] interface IAnalyzerFilter;
    }

    [uuid("19E56670-B62F-4658-BC54-086E798EF97B")]
    coclass AnalyzerPropPageConfig
    {
        [default] interface IPropertyPage;
    }

    [uuid("A18BD3EC-6CD4-472C-A1F9-A61AA6FA1D31")]
    coclass AnalyzerPropPageLog
    {
        [default] interface IPropertyPage;
    }
};
